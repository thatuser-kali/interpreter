package fnplot.syntax;

import java_cup.runtime.*;
import java.io.*;
import java.util.*;
import fnplot.sys.SyntaxFnPlotException;
import fnplot.values.FnPlotValue;

/* Preliminaries to set up and use the scanner.  */
parser code {:
		FnPlotLexer lexer;

		public FnPlotParser(FnPlotLexer l) {
		    // As of CUP v0.11, need to pass Lexer to superclass
		    super(l);
		    lexer = l;
		}

		public void report_error(String message, Object info) {
		    System.err.println(message);
		}

		public void syntax_error(Symbol cur_token) {
		    System.err.print("Line " + lexer.getLine() +
				     " near char " + lexer.getChar() + ": ");
		    report_error("Syntax error", cur_token);
		    System.err.println ("Last token read is " +
					 lexer.getText());
		}
	    :};

init with {:
	  :};

scan with {:
		try {
		    return lexer.next_token();
		} catch (java.io.IOException ioe) {
                    String msg = "Line " + lexer.getLine() + ", pos " +
				       lexer.getColumn() +
				       ": Unrecognised token <" +
				       lexer.getText() + ">";
		    throw new SyntaxFnPlotException(msg, ioe);
		}
	  :};

/* Terminals (tokens returned by the scanner). */

// special symbols
terminal LPAREN, RPAREN;

// arithmetic operators
terminal PLUS, MINUS, MUL, DIV, MOD, ASSIGN;

terminal LET, IN, LBRACE, RBRACE, COMMA;
terminal SEMI, LSBRACE, RSBRACE, COLON;

//terminal keywords
terminal FUN, CLEAR, MAPSTO, FOR, PLOT;

// terminals with values
terminal Integer INTEGER;
terminal String VARIABLE;
terminal Double REAL;

/* Non terminals */
non terminal ArithProgram program;
non terminal StmtSequence stmtList;
non terminal Statement stmt;
non terminal clearstmt; 
non terminal StmtDefinition definition;

non terminal StmtLet letExp;
non terminal ArrayList<Binding> bindings;
non terminal ArrayList<Binding> bindingsAux;
non terminal Binding binding;

non terminal Exp expression;
non terminal ExpFunction functionDef;
non terminal ArrayList<String> paramlist;
non terminal ArrayList<String> paramlistAux;
non terminal Exp arithExp;
non terminal Exp term;
non terminal Exp factor;
non terminal FnPlotValue<?> number;
non terminal empty;
non terminal Exp functionCall;
non terminal ArrayList<Exp> arglist;
non terminal ArrayList<Exp> arglistAux;
non terminal StmtPlot plotstmt;

/* Grammar rules */

program ::= stmtList:s {: RESULT = new ArithProgram(s); :};

stmtList ::= stmtList:lst stmt:s {:
		lst.add(s); 
		RESULT = lst;
		:} |
	     stmt:s {:
		RESULT = new StmtSequence(s);
		:};

stmt ::= definition:d {: RESULT = d; :} |
	 expression:e SEMI {: RESULT = e; :} |
	 plotstmt:p SEMI {:RESULT = p;:} |
	 CLEAR LPAREN RPAREN SEMI {:RESULT = null;:};

expression ::=  letExp:l {: RESULT = l; :} |
		LBRACE stmtList:body RBRACE {: RESULT = body; :} |
		arithExp:a {: RESULT = a; :} | 
		functionDef:fn {: RESULT = fn; :} | 
		functionCall:fc {:RESULT = fc; :};

plotstmt ::= PLOT expression:exp1 FOR VARIABLE:v IN LSBRACE expression:exp2 COLON expression:exp3 RSBRACE {:RESULT = new StmtPlot(exp1,v,exp2,exp3);:};

functionCall ::= LPAREN expression:e RPAREN LPAREN arglist:args RPAREN {: RESULT = new ExpFunctionCall(e, args) ;:} |
VARIABLE:v LPAREN arglist:args RPAREN {: RESULT = new ExpFunctionCall(new ExpVar(v), args) ;:};

arglist ::= expression:e arglistAux:args {: args.add(e); RESULT = args; :} | empty {: RESULT = new ArrayList<Exp>();:};

arglistAux ::= COMMA expression:e arglistAux:args {: args.add(e); RESULT = args; :} | empty {: RESULT = new ArrayList<Exp>();:};

functionDef ::= FUN LPAREN paramlist:p RPAREN MAPSTO expression:e {: RESULT = new ExpFunction(p,e) ;:};

paramlist ::= VARIABLE:v paramlistAux:p {:p.add(v); RESULT = p;:} | empty {: RESULT = new ArrayList<String>();:};

paramlistAux ::= COMMA VARIABLE:v paramlistAux:p {:p.add(v); RESULT = p;:} |
			empty {: RESULT = new ArrayList<String>();:};

letExp ::= LET bindings:bs IN expression:body {:
			RESULT = new StmtLet(bs, body);
           :};

binding ::= VARIABLE:v ASSIGN expression:e {:
		RESULT = new Binding(v, e);
	    :};

bindings ::= binding:b bindingsAux:bl {:
		bl.add(0,b);
		RESULT = bl;
	     :} |
		empty {: RESULT = new ArrayList(); :};

bindingsAux ::= COMMA binding:b bindingsAux:bl {:
		  bl.add(0,b);
		  RESULT = bl;
		:} |
		empty {: RESULT = new ArrayList(); :};

definition ::= VARIABLE:v ASSIGN expression:e SEMI {:
		   RESULT = new StmtDefinition(v, e);
		:};

arithExp ::= 	arithExp:e PLUS term:t {:
			RESULT = new ExpAdd(e, t); :} |
		arithExp:e MINUS term:t {:
			RESULT = new ExpSub(e, t); :} |
		term:t {: RESULT = t; :};

term ::= term:t MUL factor:f {:
		RESULT = new ExpMul(t, f); :} |
	 term:t DIV factor:f {:
		RESULT = new ExpDiv(t, f); :} |
	 term:t MOD factor:f {:
		RESULT = new ExpMod(t, f); :} |
	 factor:f {: RESULT = f; :};

number ::= INTEGER:ilit {: RESULT = FnPlotValue.make(ilit); :} | REAL:ilit {: RESULT = FnPlotValue.make(ilit);:}|
		MINUS INTEGER:ilit {:RESULT = FnPlotValue.make(-ilit);:} | MINUS REAL:ilit {:RESULT = FnPlotValue.make(-ilit);:};

factor ::= number:n {: RESULT = new ExpLit(n); :} |
	   VARIABLE:var {: RESULT = new ExpVar(var); :} |
	   LPAREN expression:e RPAREN {: RESULT = e; :}	;

empty ::= ;
